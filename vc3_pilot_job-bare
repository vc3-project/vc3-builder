#! /usr/bin/env perl

use v5.009;
use strict;
use warnings;

use autodie;
use English qw/-mo_match_vars/;
use Getopt::Long qw/GetOptions/;

my $vc3_root       = $ENV{VC3_ROOT}       || "vc3_root";
my $vc3_distfiles  = $ENV{VC3_DISTFILES}  || "$vc3_root/distfiles";
my $vc3_repository = $ENV{VC3_REPOSITORY} || "http://download.virtualclusters.org/repository";

my $vc3_user_home = $ENV{VC3_USER_HOME}  || "$vc3_root/vc3_user_home";
my $vc3_tmp_dir   = $ENV{VC3_TMP}        || "$vc3_root/tmp";

my %vc3_environment;

my @external_dependencies = qw/sh tail tar uname patch gcc g++ ld pkconfig/;


main();

sub usage {
    return <<EOF;
$0 [options] [--catalog software-bag] goal-environment [-- command-and-args]

    environment-bag defaults to ./vc3_catalog.json
    command-and-args    defaults to /bin/sh

    options are:
    --make-jobs=n    default is 2
    --sh-on-error    on building error, run /bin/sh with the build environment

Examples:
    $0 parrot -- parrot_run python examples/read-file-from-cvmfs.py
    $0 python -- /bin/sh

EOF
}

sub main {
    my $catalog      = 'vc3_catalog.json';
    my $print_help   = 0;
    my $make_jobs    = 2;
    my $sh_on_error  = 0;
    my $goal_env_version = 0;
    my $original_command = join(' ', $0, @ARGV);

    GetOptions(
        "catalog=s"      => \$catalog,
        "goal-version=s" => \$goal_env_version,
        "make-jobs=n"    => \$make_jobs,
        "sh-on-error"    => \$sh_on_error,
        "help"           => \$print_help,
    );

    if($print_help) {
        print usage();
        exit 0;
    }

    my ($goal_env_name, @command_and_args) = @ARGV;

    if(!$goal_env_name) {
        print "No goal environment given.\n\n" . usage();
        exit 1;
    }

    # set defaults
    if(!@command_and_args) {
        @command_and_args = ('/bin/sh',);
    }

    # clean this!
    %ENV = (
        VC3_COMMAND_LINE => File::Spec->rel2abs(join(" ", $original_command)),
        PATH      => "/bin:/usr/bin",
        MAKEFLAGS => "-j${make_jobs}",
        TERM      => 'linux'
    );

    # clean this! set default path more cleanly (see also PATH above)
    $vc3_environment{PATH}->{'/bin'}     = 1;
    $vc3_environment{PATH}->{'/usr/bin'} = 1;

    set_vc3_directories();

    # disable stdout buffering
    select(STDOUT); $OUTPUT_AUTOFLUSH = 1;

    my $bag = VC3::Bag->new($catalog);

    # bag, goal, min-version, max-version
    $bag->set_environment($goal_env_name, $sh_on_error, $goal_env_version, $goal_env_version);

    print "building phase done!\n";

    chdir $vc3_user_home;
    exec qq(@command_and_args);
}

sub set_vc3_directories {
    $vc3_root      = File::Spec->rel2abs("${vc3_root}");
    $vc3_distfiles = File::Spec->rel2abs("${vc3_distfiles}");
    $vc3_user_home = File::Spec->rel2abs("${vc3_user_home}");
    $vc3_tmp_dir   = File::Spec->rel2abs("${vc3_tmp_dir}");

    File::Path::make_path($vc3_root);
    File::Path::make_path($vc3_distfiles);
    File::Path::make_path("$vc3_distfiles/restricted-license");
    File::Path::make_path($vc3_user_home);
    File::Path::make_path($vc3_tmp_dir);

    $ENV{VC3_ROOT}      = $vc3_root;
    $ENV{VC3_DISTFILES} = $vc3_distfiles;
    $ENV{HOME}          = $vc3_user_home;
    $ENV{TMP}           = $vc3_tmp_dir;
}

package VC3::Bag; 

use Carp qw/carp croak/;
use English qw/-mo_match_vars/;
use File::Spec ();
use version ();
use POSIX ();

use JSON::Tiny;

use Data::Dumper;

sub new {
    my ($class, $catalog) = @_;

    my $self = bless {}, $class;
    $self->set_machine_vars();

    $self->{sources} = $self->decode_bag($catalog);

    return $self;
}

sub set_machine_vars {
    my ($self) = @_;

    ($self->{osname}, undef, undef, undef, $self->{architecture}) = POSIX::uname();

    $self->{target} = $self->architecture . '/' . $self->distribution;

    my $ldd_version_raw = qx(ldd --version);
    $ldd_version_raw =~ /
    # we are looking for a line starting with 'ldd'
    ^ldd
    # followed by anything
    .*
    # followed by at least one space
    \s+
    # followed by the version number (that we capture)
    ([0-9.]+)
    # followed by any number of spaces at the end of the line
    \s*$
    # options: x allows regexp verbose. m treats each line indepedently
    /xm ;

    $self->{glibc_version} = $1
    || 'unknown';

    $ENV{VC3_MACHINE_OS}   = $self->osname;
    $ENV{VC3_MACHINE_ARCH} = $self->architecture;
    $ENV{VC3_MACHINE_GLIBC_VERSION} = $self->glibc_version;
}

sub target {
    my ($self) = @_;
    return $self->{target};
}

sub osname {
    my ($self) = @_;
    return $self->{osname};
}

sub architecture {
    my ($self) = @_;
    return $self->{architecture};
}

sub glibc_version {
    my ($self) = @_;
    return $self->{glibc_version};
}

# reads /etc/readhat-release and transforms something like:
# 'Red Hat Enterprise Linux Server release 6.5 (Santiago)'
# into 'redhat6'.
sub distribution {
    my ($self) = @_;

    my $distribution='generic';
    
    if (-f '/etc/redhat-release') {
        open (my $file_fh, '<', '/etc/redhat-release');
        my $redhat_version_line = <$file_fh>;

        $redhat_version_line =~ /\brelease\b\s+([0-9]+)\b/;
        my $redhat_version = $1;

        die 'Could not find redhat version!' unless $redhat_version;

        $distribution="redhat${redhat_version}"
    }
    
    return $distribution;
}

sub widget {
    my ($self, $widget_name) = @_;

    return $self->{sources}{$widget_name};
}

sub decode_bag {
    my ($self, $filename) = @_;
    open( my $catbag_f, '<:encoding(UTF-8)', $filename); 
    my $contents = do { local($/); <$catbag_f> };
    close($catbag_f);

    my $bag_raw = JSON::Tiny::decode_json($contents);

    my $sources = {};
    
    for my $widget_name (keys %{$bag_raw}) {
        my @versions = map { VC3::Widget->new($self, $_) } @{$bag_raw->{$widget_name}};
        $sources->{$widget_name} = \@versions;
    }

    return $sources;
}

sub set_environment {
    my ($self, $widget_name, $sh_on_error, $min_version, $max_version) = @_;

    my $widget_versions = $self->widget($widget_name)
    || die "I do not know anything about '$widget_name' . \n";

    my $widget = select_version($widget_name, $widget_versions, $min_version, $max_version);

    # set root of this widget, in case dependencies need to refer to its parent.
    $ENV{$widget->var_root_name} = $widget->destination_dir;
    
    $self->set_dependencies($sh_on_error, $widget->dependencies);
    $widget->set_environment_variables();

    my $exit_status = 0;
    if($widget->sources) { 
        for my $source (@{$widget->sources}) {
            # save current environment so that we can rollback in case of error.
            my %old_ENV = %ENV;
            my %old_vc3_environment = %vc3_environment;

            # set dependencies particular to this source
            $self->set_dependencies($sh_on_error, $source->dependencies);

            eval {$exit_status = -1; $exit_status = $widget->execute_recipe($source);};

            if($exit_status) {
                # on error, reestablish old environment, and show last lines of log
                # file.
                %ENV = %old_ENV;
                my %vc3_environment = %old_vc3_environment;

                $widget->process_error($sh_on_error, $EVAL_ERROR, $exit_status);
            } else {
                # on success, we keep break out and do no try more recipes.
                last;
            }
        }
    }

    # last of the tries also failed, so we report failure for this widget.
    if($exit_status) {
        die "Could not set environment for '" . $widget->name . "' succesfully' . \n";
    }
}

sub set_dependencies {
    my ($self, $sh_on_error, $dependencies) = @_;

    if($dependencies) {
        for my $dependency (keys %{$dependencies}) {
            my ($min_dep_ver, $max_dep_ver) = @{$dependencies->{$dependency}};
            $self->set_environment($dependency, $sh_on_error, $min_dep_ver, $max_dep_ver);
        }
    }
}

# versions available, min desired version, max desired version
sub select_version {
    my ($pkg_name, $pkg_versions, $min_version, $max_version) = @_;

    if($min_version) {
        $min_version = version->parse($min_version);
    }

    if($max_version) {
        $max_version = version->parse($max_version);
    }

    my $min_found;
    my $max_found;

    for my $pkg_version (@{$pkg_versions}) {
        if(!$min_version) {
            return $pkg_version;
        }

        my $version = version->parse($pkg_version->{version});

        $min_found = $version unless $min_found;
        $max_found = $version unless $max_found;

        $min_found = $min_found lt $version ? $version : $min_found;
        $max_found = $max_found gt $version ? $version : $max_found;

        next if $min_version gt $version;

        if(!$max_version) {
            return $pkg_version;
        }

        next if $max_version lt $version;
        return $pkg_version;
    }

    $max_version = $min_version unless $max_version;
    die "I did not find a suitable version for '$pkg_name'.\n Found: ["
    . version::normal($min_found)
    . ',' . version::normal($max_found)
    . ']. Wanted ['
    . $min_version->normal
    . ',' . $max_version->normal
    . "]\n";
}

sub expand_env_variable {
    my ($varvalue) = @_;

    $varvalue =~ s/\$\{(\w+)\}/env_variable_value($1)/eg;

    return $varvalue;
}

sub env_variable_value {
    my ($varname) = @_;

    my $value = $ENV{$varname}
    || die "Variable '$varname' has not been defined.";
}


package VC3::Widget;
use Carp;
use POSIX ":sys_wait_h";

use Data::Dumper;

# Attributes:
# name, version, sources, dependencies, environment_variables
sub new {
    my ($class, $bag, $json_description) = @_;

    my $self = bless {}, $class;

    $self->bag($bag);
    $self->name($json_description->{name});
    $self->version($json_description->{version});
    $self->sources($json_description->{sources});
    $self->dependencies($json_description->{dependencies});
    $self->environment_variables($json_description->{environment_variables});

    add_root_variable($self);

    return $self;
}

sub add_root_variable {
    my ($self) = @_;

    my $vars      = $self->environment_variables || [];
    my $root_name = 'VC3_' . uc($self->name) . '_ROOT';

    my $root_var = {
        name     => $root_name,
        value    => $self->destination_dir,
        clobber  => 1,
        absolute => 1
    };

    unshift @{$vars}, $root_var;

    $self->var_root_name($root_name);
    $self->environment_variables($vars);
}

sub var_root_name {
    my ($self, $new_var_root_name) = @_;

    $self->{var_root_name} = $new_var_root_name if($new_var_root_name);

    return $self->{var_root_name};
}

sub bag {
    my ($self, $new_bag) = @_;

    $self->{bag} = $new_bag if($new_bag);

    croak 'No bag given'
    unless($self->{bag}); 

    return $self->{bag};
}

sub name {
    my ($self, $new_name) = @_;

    $self->{name} = $new_name if($new_name);

    croak 'No name given'
    unless($self->{name}); 

    return $self->{name};
}

sub version {
    my ($self, $new_version) = @_;

    $self->{version} = $new_version if($new_version);

    croak 'No version given'
    unless($self->{version}); 

    return $self->{version};
}

sub dependencies {
    my ($self, $new_dependencies) = @_;

    $self->{dependencies} = $new_dependencies if($new_dependencies);

    return $self->{dependencies};
}

sub sources {
    my ($self, $new_sources) = @_;

    if($new_sources) {
        my @sources = map { VC3::Source->new($self, $_) } @{$new_sources};
        $self->{sources} = \@sources;
    }

    return $self->{sources};
}

sub environment_variables {
    my ($self, $new_vars) = @_;

    $self->{environment_variables} = $new_vars if($new_vars);

    return $self->{environment_variables};
}

sub destination_dir {
    my ($self) = @_;
    return File::Spec->rel2abs($vc3_root) . '/' . $self->bag->target . '/' . $self->name . '-' . $self->version;
}

sub build_dir {
    my ($self) = @_;
    return File::Spec->rel2abs($vc3_root) . '/builds/' . $self->name;
}

sub build_log {
    my ($self) = @_;
    my $log_name = $self->destination_dir . '/' . $self->name . '-build-log';

    return $log_name;
}

sub completion_ribbon {
    my ($self) = @_;
    my $ribbon = $self->destination_dir . '/.VC3_DEPENDENCY_BUILD_COMPLETE';

    return $ribbon;
}

sub set_environment_variables {
    my ($self) = @_; 

    my $vars = $self->environment_variables
    || return;

    for my $var (@{$vars}) {

        my $name = $var->{name}
        || carp "Environment variable does not have a name.";

        my $value = $var->{value}
        || carp "Environment variable '$name' did not define a value.";

        my $clobber  = $var->{clobber};
        my $absolute = $var->{absolute};

        # split paths on : if necessary
        my @paths;
        if($clobber) {
            $vc3_environment{$name} = {};
            @paths = ($value);
        } else {
            @paths = split /:/, $value;
        }

        for my $path (@paths) {
            if(!$absolute) {
                $path = $self->destination_dir . '/' . $path;
            }
            $path = VC3::Bag::expand_env_variable($path);
            $vc3_environment{$name}->{$path} = 1;
        }

        my $dotted = join(':', sort { $a cmp $b } keys %{$vc3_environment{$name}});
        $ENV{$name} = $dotted;
    }
}

# range is: [min_version, max_version];
# max_version is the prefered version. 
# [version] is considered as [min_version]
# sub check_version {
#     my ($self, @range) = @_;
# 
#     return 1 unless $self->versioncmd;
#     return 1 unless $self->version || @range > 0;
# 
#     if(@range == 0) {
#         @range = (version->parse($self->{version}));
#     }
# 
#     my $reg = $self->{versionreg} ? $self->{versionreg} : qr/(.*)/;
# 
#     my $version_output = qx/$self->{versioncmd}/;
# 
#     return 0 unless $version_output =~ $reg;
# 
#     my ($observed_version) = version->parse("$1");
# 
#     print $observed_version . "\n";
# 
#     return 0 if $observed_version < $range[0];
#     return 0 if $observed_version > $range[$#range];
# 
#     return $observed_version;
# }

sub error_debug_info {
    my ($self) = @_;

    print "'", $self->name, "' failed to build for ", $self->bag->target, "\n";

    print "Last lines of log file:\n";
    system('tail', $self->build_log);
}


sub process_error {
    my ($self, $sh_on_error, $eval_error, $status) = @_;

    if($eval_error || $status) {
        $self->error_debug_info;

        if($sh_on_error) {
            warn $@ if $@;
            my $pid = fork();
            if($pid == 0) {
                exec("/bin/sh -c 'cd " . $self->build_dir . " && exec \${SHELL:-/bin/sh}'");
                exit -1;
            } elsif($pid > 0) {
                waitpid $pid, 0;
            } else {
                croak 'Could not fork for debugging shell.';
            }
        }
    }
}

sub create_ribbon {
    my ($self) = @_;

    open (my $ribbon_fh, '>', $self->completion_ribbon);
    close $ribbon_fh;
}

sub prepare_recipe_sandbox {
    my ($self, $source) = @_;

    # clear build directory, to avoid bugs from uncleaned sources.
    my $build = $self->build_dir;
    if( -d $build ) {
        File::Path::rmtree($build);
    }

    # clear destination directory, to make sure we are running what we believe
    # we are running.
    my $destination = $self->destination_dir;
    if( -d $destination ) {
        File::Path::rmtree($destination);
    }

    # create the dirs we cleared above.
    File::Path::make_path($self->build_dir);
    File::Path::make_path($self->destination_dir);

    # make sure tmp dir exists
    File::Path::make_path($ENV{'TMPDIR'});

    # download to $vc3_distfiles the ingredient (i.e., input files) if missing.
    $source->get_files();

    # if generic, copy all files to build directory.
    # if tarball, expand first file to build directory, and copy the rest of
    # the files to build directory.
    $source->prepare_files($build);
}

sub execute_recipe {
    my ($self, $source) = @_;

    # if nothing to do, so we return no error:
    return 0 if -f $self->completion_ribbon();

    $self->prepare_recipe_sandbox($source);

    # set the destination directory as an environment variable before
    # setting up the shell, so that the child created inherets it.
    $ENV{'VC3_PREFIX'} = $self->destination_dir;
    $ENV{'VC3_BUILD'}  = $self->build_dir;

    my ($build_in, $pid) = $self->setup_build_shell($source);

    my @steps = @{$source->recipe};

    # add shifting to build directory as the first step.
    unshift @steps, 'cd ' . $self->build_dir;

    # add exiting cleanly from shell as a last step.
    push @steps, 'exit 0';

    print "Building for '" . $self->name . "' for ", $self->bag->target, "\n";
    print "for building details:\ntail -f " . $self->build_log . "\n";

    for my $step (@steps) {
        my $date = gmtime();
        print { $build_in } ": $date\n";
        print { $build_in } "$step\n";
    }

    my $status = -1;
    eval { close $build_in; $status = $? };

    delete $ENV{VC3_PREFIX}; 
    delete $ENV{VC3_BUILD}; 

    if(!$@ && WIFEXITED($status) && (WEXITSTATUS($status) == 0)) {
        $self->create_ribbon();
        return 0;
    } else {
        return -1;
    }
}

sub setup_build_shell {
    my ($self, $source) = @_;

    my ($build_log, $build_in);

    # log the recipe used. Since we are opening sh with -e, the recipe executes
    # as if all steps were &&-ed together.
    open $build_log,    '>', $self->build_log;
    print { $build_log } join("\n && ", @{$source->recipe}) . "\n";
    close $build_log;

    # open sh with -e. This terminates the shell at the first step that returns
    # a non-zero status.
    my $pid = open($build_in, '|-', '/bin/sh -e');

    croak 'Could not open /bin/sh for building.'
    unless $build_in;

    # redirect all output to our log file.
    print { $build_in } 'exec 1>> ' . $self->build_log . "\n";
    print { $build_in } "exec 2>&1\n";

    # print some debug shell information (e.g., the step about to be executed).
    print { $build_in } "set -ex\n";

    # return the stdin of the shell, and the pid so we can wait for it.
    return ($build_in, $pid);
}

package VC3::Source;
use Carp;
use Data::Dumper;

sub new {
    my ($class, $widget, $source_raw) = @_;

    my $source;

    if($source_raw->{type} eq 'generic') {
        $source = VC3::Source::Generic->new($widget, $source_raw);
    }
    elsif($source_raw->{type} eq 'tarball') {
        $source = VC3::Source::Tarball->new($widget, $source_raw);
    }
    elsif($source_raw->{type} eq 'license-restricted') {
        $source = VC3::Source::Restricted->new($widget, $source_raw);
    }
    elsif($source_raw->{type} eq 'service') {
        croak "Not implemented";
    }
    else {
        croak "Do not know about source type '" . $source_raw->{type} . "' for '" . $widget->name . "'";
    }

    return $source;
}

package VC3::Source::Generic;
use Carp;
use File::Copy;
use File::Path;
use File::Spec;
use HTTP::Tiny;

use Data::Dumper;

sub new {
    my ($class, $widget, $json_description) = @_;

    my $self = bless {}, $class;

    $self->widget($widget);
    $self->recipe($json_description->{recipe});
    $self->files($json_description->{files});
    $self->message($json_description->{message});
    $self->dependencies($json_description->{dependencies});

    return $self;
}

sub widget {
    my ($self, $new_widget) = @_;

    $self->{widget} = $new_widget if($new_widget);

    croak 'No environment argument given'
    unless($self->{widget}); 

    return $self->{widget};
}

sub recipe {
    my ($self, $new_recipe) = @_;

    $self->{recipe} = $new_recipe if($new_recipe);

    croak 'No recipe given'
    unless($self->{recipe}); 

    return $self->{recipe};
}

sub files {
    my ($self, $new_files) = @_;

    $self->{files} = $new_files if($new_files);

    if($self->{files}) {
        return $self->{files};
    } else {
        return [];
    }
}

sub message {
    my ($self, $new_message) = @_;

    if($new_message) {
        my @expansions = map { VC3::Bag::expand_env_variable($_) } @{$new_message};
        $self->{message} = join("\n", @expansions);
        $self->{message} = "\n" . ${self}->{message};
    }

    return $self->{message};
}


sub file_absolute {
    my ($self, $file) = @_;
    return File::Spec->rel2abs($vc3_distfiles . '/' . $file);
}

sub dependencies {
    my ($self, $new_dependencies) = @_;

    $self->{dependencies} = $new_dependencies if($new_dependencies);

    return $self->{dependencies};
}

sub get_file {
    my ($self, $file) = @_;

    unless(-f $self->file_absolute($file)) {
        print "Downloading '" . $file . "' from $vc3_distfiles\n";

        #if make sure the destination directory exists
        File::Path::make_path($vc3_distfiles);

        my $ff = HTTP::Tiny->new();

        my $url    = $vc3_repository . '/' . $file;
        my $output = $vc3_distfiles  . '/' . $file;

        my $response = $ff->mirror($url, $output);

        # 304 means 'unchanged'
        unless($response->{success} || $response->{status} eq 304 ) { 
            die "Could not download '" . $file . "'\n";
        }
    }
}

sub get_files {
    my ($self) = @_;

    my $files = $self->files;

    for my $file (@{$files}) {
        $self->get_file($file);
    }
}

sub prepare_files {
    my ($self, $build_dir) = @_;

    for my $file (@{$self->files}) {
        symlink($self->file_absolute($file), $build_dir . '/' . $file)
        || die "Could not link '" . $file . "' to build directory.\n";
    }
}

package VC3::Source::Tarball;
use base 'VC3::Source::Generic';
use Carp;

sub new {
    my ($class, $widget, $json_description) = @_;

    my $self = VC3::Source::Generic->new($widget, $json_description);

    unless($self->files) {
        croak "For type 'tarball', at least one file should be defined in the files list, and the first file in the list should be a tarball.";
    }

    $self = bless $self, $class;

    return $self;
}

sub prepare_files  {
    my ($self, $build_dir) = @_;

    # first file in $self->files is the tarball, by convention.
    my $tarball = @{$self->files}[0];
    $tarball = $self->file_absolute($tarball);

    print "Expanding $tarball\n";
    system(qq/tar -C ${build_dir} --strip-components=1 -xpf ${tarball}/);
    die "Could not expand tarball $tarball.\n" if $?;

    # link in the rest of the input files.
    $self->SUPER::prepare_files($build_dir);
}

package VC3::Source::Restricted;
use base 'VC3::Source::Generic';
use Carp;

sub get_file {
    my ($self, $file) = @_;

    unless(-f $self->file_absolute($file)) {
        print $self->message . "\n\n";
        die "Missing restricted-license file.\n";
    }
}

sub file_absolute {
    my ($self, $file) = @_;
    return File::Spec->rel2abs($vc3_distfiles . '/restricted-license/'  . $file);
}

